apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'maven-publish'

dependencies {
    compile project(':repository')
    runtime 'javax.servlet:jstl:1.2'
    providedCompile 'javax.servlet:servlet-api:2.5'
}

gretty {
    contextPath = '/todo'
}

task createBuildInfoFile {
    doLast {
        def buildInfoFile = new File("$buildDir/build-info.properties")
        Properties props = new Properties()
        props.setProperty('version', project.version.toString())
        props.setProperty('timestamp', project.buildTimestamp)
        props.store(buildInfoFile.newWriter(), null)
    }
}

war {
    dependsOn createBuildInfoFile

    baseName = 'todo'

    from(buildDir) {
        include 'build-info.properties'
        into('WEB-INF/classes')
    }

}

ext {
    // 通过 ${HOME}/.gradle/gradle.properties 文件进行配置，避免敏感信息泄露
    nexusReleaseUrl = mavenNexusReleaseUrl
    nexusSnapshotUrl = mavenNexusSnapshotUrl
    nexusUsername = mavenNexusUsername
    nexusPassword = mavenNexusPassword
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.web
            artifactId 'todo-web'

            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'To Do application')
                root.appendNode('description', 'A simple task management application.')
            }
        }
    }

    repositories {
        maven {
            name 'nexus'
            url project.version.toString().endsWith('-SNAPSHOT') ? nexusSnapshotUrl : nexusReleaseUrl

            credentials {
                username = nexusUsername
                password = nexusPassword
            }
        }
    }
}

