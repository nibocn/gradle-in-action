configurations {
    cargo {
        description = 'Classpath for Cargo Ant tasks.'
        visible = false
    }
}

task deployToLocalTomcat {
    doLast {
        FileTree cargoDeps = configurations.getByName('cargo').asFileTree
        ant.taskdef(resource: 'cargo.tasks', classpath: cargoDeps.asPath)

        ant.cargo(containerId: 'tomcat7x', action: 'run', output: "$buildDir/output.log") {
            configuration {
                deployable(type: 'war', file: 'todo.war')
            }

            zipUrlInstaller(installUrl: 'http://apache.fayea.com/tomcat/tomcat-7/v7.0.79/bin/apache-tomcat-7.0.79.zip')
        }
    }
}

ext.cargoGroup = 'org.codehaus.cargo'
ext.cargoVersion = '1.3.1'

dependencies {
    cargo "$cargoGroup:cargo-ant:$cargoVersion"
}

repositories {
    maven {
        name 'Aliyun Maven Repository'
        url 'http://maven.aliyun.com/nexus/content/groups/public'
    }
    mavenCentral()
}

task printDependencies {
    doLast {
        configurations.cargo.each { dependency -> 
            println dependency
        }
    }
}

configurations.cargo.resolutionStrategy {
    force "$cargoGroup:cargo-ant:1.3.0"
    force "xml-apis:xml-apis:1.0.b2"
}
