plugins {
    id 'org.akhikhl.gretty' version '2.0.0' apply false
}

def env = project.hasProperty('env') ? project.getProperty('env') : 'local'
logger.quiet "Loading configuration for environment '$env'."

def configFile = file("$rootDir/gradle/config/buildConfig.groovy")
def parsedConfig = new ConfigSlurper(env).parse(configFile.toURI().toURL())


allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    group = 'me.nibo.todo'
    version = '0.1'

    apply from: "$rootDir/gradle/versioning.gradle"

    ext.config = parsedConfig

    idea {
        module {
            downloadSources = false
            downloadJavadoc = false
        }
    }

    eclipse {
        classpath {
            downloadSources = false
            downloadJavadoc = false
        }
    }
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    tasks.withType(Test) {
        testLogging {
            exceptionFormat 'full'
        }
    }
}