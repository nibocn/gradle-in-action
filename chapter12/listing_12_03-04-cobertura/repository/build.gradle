plugins {
    id 'net.saliman.cobertura' version '2.5.0'
}
apply from: 'databaseSetup.gradle'

dependencies {
    compile project(':model')
    runtime 'com.h2database:h2:1.4.196'
    testCompile 'junit:junit:4.12'
    testRuntime "org.slf4j:slf4j-api:1.7.10"
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integTest/java')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

tasks.withType(Test) {
    testLogging {
        exceptionFormat 'full'
    }
}

task integrationTest(type: Test) {
    group = 'verification'
    description = 'Runs the integration tests.'
    testClassesDirs = files(sourceSets.integrationTest.output.classesDirs)
    classpath = sourceSets.integrationTest.runtimeClasspath
}

startDatabase.dependsOn test
integrationTest.dependsOn startAndPrepareDatabase
stopDatabase.dependsOn integrationTest
check.dependsOn stopDatabase