plugins {
    id 'org.akhikhl.gretty' version '2.0.0' apply false
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'

    group = 'me.nibo.todo'
    version = '0.1'

    idea {
        module {
            downloadSources = false
            downloadJavadoc = false
        }
    }

    eclipse {
        classpath {
            downloadSources = false
        }
    }
}

subprojects {
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }
}

cleanIdea.dependsOn cleanIdeaWorkspace

idea {
    project {
        jdkName = '1.6'
        languageLevel = '1.6'

        ipr.withXml { provider ->
            def node = provider.asNode()
            def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])

            gradleSettings.appendNode('option', [name: 'linkedProjectPath',
                                        value: '$PROJECT_DIR$/build.gradle'])
        }
    }
}
